(require 'cl-ppcre)
(require 'uiop)

(defun read-logic-file (filename)
  (let ((lut (make-hash-table :test #'equal))
        (max-z 0))
    (mapc (lambda (row)
            (cl-ppcre:register-groups-bind (wire (#'parse-integer wire-value)) ("(.+): (.+)" row)
              (setf (gethash wire lut) wire-value))
            (cl-ppcre:register-groups-bind (expression wire) ("(.+) -> (.+)" row)
              (setf (gethash wire lut) expression)
              (when (eq (char wire 0) #\z)
                (setf max-z (max max-z (parse-integer (subseq wire 1)))))))
          (uiop:read-file-lines filename))
    (values lut max-z)))

(defun eval-symbol (sym lut)
  (let ((expr (gethash sym lut)))
    (unless (numberp expr)
      (cl-ppcre:register-groups-bind (wire-a operator wire-b) ("(.+) (.+) (.+)" expr)
        (setf expr (funcall (cond ((string= operator "AND") #'logand)
                                  ((string= operator "OR") #'logior)
                                  ((string= operator "XOR") #'logxor))
                            (eval-symbol wire-a lut)
                            (eval-symbol wire-b lut))))
      (setf (gethash sym lut) expr))
    expr))

(defun eval-logic-file (filename)
  (multiple-value-bind (lut max-z) (read-logic-file filename)
    (labels ((eval-z (n)
               (+ (ash (eval-symbol (format nil "z~2,'0d" n) lut) n)
                  (if (= n 0) 0 (eval-z (1- n))))))
      (eval-z max-z))))


(assert (= (eval-logic-file "test.txt") 4))
(assert (= (eval-logic-file "test2.txt") 2024))
(assert (= (print (eval-logic-file "input.txt")) 36902370467952))
