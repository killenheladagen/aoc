(require 'cl-ppcre)
(load "board")

(defun read-coordinate-file (filename)
  (mapcar (lambda (xy) (complex (car xy) (cadr xy)))
          (mapcar (lambda (line) (mapcar #'parse-integer (cl-ppcre:split "," line)))
                  (uiop:read-file-lines filename))))

(defun fill-in-distance (pos sb b)
  (labels ((step-in-dir (dir) (+ pos dir))
           (wall-or-outside (pos) (or (outside-board pos b) (wall-p pos)))
           (wall-p (pos) (eq (at pos b) #\#)))
    (let* ((new-distance (1+ (at pos sb)))
           (adjacent-pos (mapcar #'step-in-dir *north-east-south-west*))
           (pos-to-score (remove-if #'wall-or-outside adjacent-pos)))
      (mapc (lambda (pos) (fill-in-distance pos sb b))
            (mapcan (lambda (pos)
                      (let ((old-distance (at pos sb)))
                        (when (or (not old-distance) (< new-distance old-distance))
                          (set-at new-distance pos sb)
                          (list pos))))
                    pos-to-score)))))

(defun length-of-shortest-path (start end b)
  (let ((sb (make-empty-board (board-dimensions b))))
    (set-at 0 end sb)
    (fill-in-distance end sb b)
    ;;(print-board sb #'print-int-cell)
    (at start sb)))

(defun minimum-steps-to-reach-exit (size num-bytes filename)
  (let ((b (make-empty-board (complex size size) :initial-element #\.)))
    (mapc (lambda (pos) (set-at #\# pos b))
          (subseq (read-coordinate-file filename) 0 num-bytes))
    (length-of-shortest-path 0 (complex (1- size) (1- size)) b)))

(assert (= (minimum-steps-to-reach-exit 7 12 "test.txt") 22))
(assert (= (print (minimum-steps-to-reach-exit 71 1024 "input.txt")) 284))

(defun find-first-bad-coord (size good-num filename)
  (let ((bad-num (length (read-coordinate-file filename))))
    (flet ((is-bad (num-bytes)
             (null (minimum-steps-to-reach-exit size num-bytes filename))))
      (loop while (not (= (1+ good-num) bad-num)) do
        (let ((cand-num (floor (+ good-num bad-num) 2)))
          (format t "good=~a bad=~a cand=~a~%" good-num bad-num cand-num)
          (if (is-bad cand-num)
              (setf bad-num cand-num)
              (setf good-num cand-num)))))
    (nth (1- bad-num) (read-coordinate-file filename))))

(print (find-first-bad-coord 7 20 "test.txt"))
(print (find-first-bad-coord 71 1024 "input.txt"))
